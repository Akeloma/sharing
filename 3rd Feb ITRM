IT Audit Findings (Selected Month) = 
VAR YearIsSel  = HASONEVALUE('Calendar'[Year])
VAR MonthIsSel = HASONEVALUE('Calendar'[Month])

/* Pick a single target date based on slicers */
VAR TargetDate =
    IF(
        MonthIsSel,
        CALCULATE(
            MAX('tblAudit'[Date extracted]),
            ALLEXCEPT('Calendar', 'Calendar'[Year], 'Calendar'[Month])
        ),
        IF(
            YearIsSel,
            CALCULATE(
                MAX('tblAudit'[Date extracted]),
                ALLEXCEPT('Calendar', 'Calendar'[Year])
            ),
            CALCULATE(
                MAX('tblAudit'[Date extracted]),
                ALL('tblAudit')
            )
        )
    )

RETURN
IF(
    ISBLANK(TargetDate),
    0,
    VAR StartOfMonthh = DATE(YEAR(TargetDate), MONTH(TargetDate), 1)
    VAR EndOfMonthh   = EOMONTH(TargetDate, 0)
    VAR Result_ =
        CALCULATE(
            SUM('tblAudit'[Count]),
            KEEPFILTERS('tblAudit'[Status] = "Total IT Audit Findings"),
            DATESBETWEEN('Calendar'[Date], StartOfMonthh, EndOfMonthh)
        )
    RETURN COALESCE(Result_, 0)
)
---------------------------------------------------------

IT Audit Findings (Selected Month - Prev) =
VAR YearIsSel  = HASONEVALUE ( 'Calendar'[Year] )
VAR MonthIsSel = HASONEVALUE ( 'Calendar'[Month] )

/* Anchor date from slicer (end of selected month/year) */
VAR SlicerAnchorDate =
    IF (
        MonthIsSel,
        CALCULATE (
            MAX ( 'Calendar'[Date] ),
            ALLEXCEPT ( 'Calendar', 'Calendar'[Year], 'Calendar'[Month] )
        ),
        IF (
            YearIsSel,
            CALCULATE (
                MAX ( 'Calendar'[Date] ),
                ALLEXCEPT ( 'Calendar', 'Calendar'[Year] )
            ),
            BLANK ()
        )
    )

/* Previous month when month is selected */
VAR PrevMonthAnchor =
    IF ( MonthIsSel, EOMONTH ( SlicerAnchorDate, -1 ), BLANK () )

/* Choose target date
   - Month selected -> previous month
   - Year only -> latest extract within that year
   - Nothing -> latest extract overall
*/
VAR TargetDate =
    IF (
        MonthIsSel,
        PrevMonthAnchor,
        IF (
            YearIsSel,
            CALCULATE (
                MAX ( 'tblAudit'[Date extracted] ),
                FILTER ( ALL ( 'tblAudit' ), YEAR ( 'tblAudit'[Date extracted] ) = YEAR ( SlicerAnchorDate ) )
            ),
            CALCULATE ( MAX ( 'tblAudit'[Date extracted] ), ALL ( 'tblAudit' ) )
        )
    )

RETURN
IF (
    ISBLANK ( TargetDate ),
    0,
    VAR StartOfMonth = DATE ( YEAR ( TargetDate ), MONTH ( TargetDate ), 1 )
    VAR EndOfMonth   = EOMONTH ( TargetDate, 0 )
    VAR Result_ =
        CALCULATE (
            SUM ( 'tblAudit'[Count] ),
            KEEPFILTERS ( 'tblAudit'[Status] = "Total IT Audit Findings" ),
            DATESBETWEEN ( 'Calendar'[Date], StartOfMonth, EndOfMonth )
        )
    RETURN COALESCE ( Result_, 0 )
)
